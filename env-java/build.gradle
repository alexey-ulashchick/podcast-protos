plugins {
	id 'com.google.protobuf' version '0.8.10'
	id 'java'
	id 'maven-publish'
}

def majorVersion = '1'
def minorVersion = '0'
def patchVersion = System.getenv("GITHUB_RUN_NUMBER") ?: '0'

group = 'com.ulashchick.podcast'
version = majorVersion + '.' + minorVersion + '.' + patchVersion
sourceCompatibility = 1.8

sourceSets {
	main {
		proto {
			srcDir '../protos'
		}
	}
}

repositories {
	mavenCentral()
	maven { url "https://jitpack.io" }
}

dependencies {
	implementation 'com.google.protobuf:protobuf-java:3.10.0'
	implementation 'com.salesforce.servicelibs:rxgrpc-stub:1.0.1'
	implementation 'io.grpc:grpc-all:1.25.0'
	implementation 'io.reactivex.rxjava2:rxjava:2.2.20'
	implementation 'javax.annotation:javax.annotation-api:1.3.2'
}

protobuf {
	protoc {
		artifact = 'com.google.protobuf:protoc:3.10.1'
	}
	plugins {
		grpc {
			artifact = 'io.grpc:protoc-gen-grpc-java:1.25.0'
		}
		rxgrpc {
			artifact = "com.salesforce.servicelibs:rxgrpc:1.0.1"
		}
	}
	generateProtoTasks {
		ofSourceSet("main")*.plugins {
			grpc { }
			rxgrpc {}
		}
	}
}

def pomConfig = {
	licenses {
		license {
			name 'MIT'
			url 'http://www.opensource.org/licenses/mit-license.php'
			distribution 'repo'
		}
	}
	developers {
		developer {
			id 'alexey-ulashchick'
			name 'Alexey Ulashchick'
			email 'a.ulashchick@gmail.com'
		}
	}
	scm {
		url 'https://github.com/alexey-ulashchick/podcast-protos'
	}
}

publishing {
	publications {
		mavenPublication(MavenPublication) {
			from components.java
			groupId 'com.ulashchick.podcast'
			artifactId 'protos'
			version = majorVersion + '.' + minorVersion + '.' + patchVersion
			pom.withXml {
				def root = asNode()
				root.appendNode('description', 'Protos library for Podcast Project')
				root.appendNode('name', 'podcast-protos')
				root.appendNode('url', 'https://github.com/alexey-ulashchick/podcast-protos')
				root.children().last() + pomConfig
			}
		}
	}
}
